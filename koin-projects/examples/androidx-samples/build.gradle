apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: '../../gradle/versions-examples.gradle'

android {
    compileSdkVersion android_target_version
    buildToolsVersion android_build_tools_version

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion android_target_version
        applicationId "org.koin.sample.androidx"
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            useJUnit()
        }
    }
}

dependencies {

    // Koin
    implementation project(":koin-androidx-ext")
    implementation project(":koin-androidx-fragment")
    implementation 'junit:junit:4.12'

    testImplementation project(":koin-test")

    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.test:core:1.1.1"
    testImplementation "androidx.test.ext:junit:1.1.1"
    testImplementation "org.robolectric:robolectric:4.2.1"
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation "androidx.appcompat:appcompat:$androidx_lib_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$android_arch_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$androidx_savedstate"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"

}

task checkAndroidModules(dependsOn: 'testDebugUnitTest')

gradle.taskGraph.whenReady { taskGraph ->
    def testTask = tasks.getByName("testDebugUnitTest")

    if (taskGraph.hasTask(tasks.getByName("checkAndroidModules"))) {
        testTask.useJUnit {
            includeCategories 'org.koin.test.category.CheckModuleTest'
        }
    }
}

task checkModules(
        type: Test,
        description: 'Run integration tests only. Pass in \'-Pintegration=true\''
) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
    useJUnit {
        includeCategories 'org.koin.test.category.CheckModuleTest'
    }
}